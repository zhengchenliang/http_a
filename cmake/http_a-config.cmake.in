@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

# Required dependencies
find_dependency(OpenSSL)
find_dependency(ZLIB)
find_dependency(Threads)

# nlohmann/json
find_dependency(nlohmann_json 3.11)

# Check for h2o (user must have it installed)
find_path(H2O_INCLUDE_DIR NAMES h2o.h)
find_library(H2O_LIBRARY NAMES h2o libh2o.a)
if(NOT H2O_INCLUDE_DIR OR NOT H2O_LIBRARY)
  message(FATAL_ERROR "h2o library not found. Please install h2o or set H2O_ROOT.")
endif()

# Check for libuv
find_path(UV_INCLUDE_DIR NAMES uv.h)
find_library(UV_LIBRARY NAMES uv libuv)
if(NOT UV_INCLUDE_DIR OR NOT UV_LIBRARY)
  message(FATAL_ERROR "libuv not found. Please install libuv.")
endif()

# Check for brotli
find_path(BROTLI_INCLUDE_DIR NAMES brotli/decode.h)
find_library(BROTLI_DEC_LIBRARY NAMES brotlidec)
find_library(BROTLI_COMMON_LIBRARY NAMES brotlicommon)
if(NOT BROTLI_INCLUDE_DIR OR NOT BROTLI_DEC_LIBRARY)
  message(FATAL_ERROR "Brotli not found. Please install libbrotli.")
endif()

include("${CMAKE_CURRENT_LIST_DIR}/http_a-targets.cmake")

# Add nlohmann_json to the imported target (was BUILD_INTERFACE only)
if(TARGET http_a::http_a)
  set_property(TARGET http_a::http_a APPEND PROPERTY
    INTERFACE_LINK_LIBRARIES nlohmann_json::nlohmann_json
  )
endif()

check_required_components(http_a)

